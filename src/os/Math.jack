class Math {
  static Array two_to_the_power;

  function void init() {
    var int i;

    let two_to_the_power = Array.new(16);
    let i = 1;
    let two_to_the_power[0] = 1;
    while (i < 16) {
      let two_to_the_power[i] = two_to_the_power[i-1] + two_to_the_power[i-1];
      let i = i + 1;
    }

    return;
  }

  // Note: this does someting bogus if x = -32768, because 32767 is the 
  // largest integer I can represent in 16-bit 2's complement.
  function int abs(int x) {
    if (x < (-32767)) {
      do Output.printString("Error: cannot negate -32767\n\r");
      do Sys.halt();
    }
    if (x < 0) {
      return -x;
    } else {
      return x;
    }
    // Should not get here
    do Output.printString("Error: abs should not get here\n\r");
    do Sys.halt();
    return 0;
  }

  // Hardware only supports addition and subtraction, so implement
  // multiplication in software
  function int multiply(int x, int y) {
    var int abs_x;
    var int abs_y;
    var int sum;
    var int shifted_x;
    var int ith_bit_of_y;
    var int i;    

    let i = 0;
    let sum = 0;
    let abs_x = Math.abs(x);
    let abs_y = Math.abs(y);
    let shifted_x = abs_x;
    while (i < 16) {
      let ith_bit_of_y = (two_to_the_power[i]) & abs_y;
      if (~(ith_bit_of_y = 0)) {
        let sum = sum + shifted_x;
      }
      let shifted_x = shifted_x + shifted_x;
      let i = i + 1;
    }

    if (((x > 0) & (y > 0)) | ((x < 0) & (y < 0))) {
      return sum;
    } else {
      return -sum;
    }
  }

  // returns integer part of x / y
  function int divide(int x, int y) {
    var int abs_x;
    var int abs_y;
    var int q;
    var int q_times_abs_y;
    var int abs_quotient;
    var int two_abs_y;

    // Undefined for y = 0
    // Report errors once error reporting available; for now, halt
    if (y = 0) {
      do Output.printString("Error: divide by zero\n\r");
      do Sys.halt();
    }

    if (x = 0) {
      return 0;
    }

    let abs_x = Math.abs(x);
    let abs_y = Math.abs(y);

    if (abs_y > abs_x) {
      return 0;
    }

    if (abs_x = abs_y) {
      if (((x > 0) & (y > 0)) | ((x < 0) & (y < 0))) {
        return 1;
      } else {
        return -1;
      }
    }

    let two_abs_y = abs_y + abs_y;
    
    // check if overflow, halt if detected
    if (two_abs_y < 0) {
      do Output.printString("Error: overflow in division\n\r");
      do Sys.halt();
    }

    let q = Math.divide(abs_x, two_abs_y);
    let q_times_abs_y = Math.multiply(q, abs_y);

    if ((abs_x - q_times_abs_y - q_times_abs_y) < abs_y) {
      let abs_quotient = q + q;
    } else {
      let abs_quotient = q + q + 1;
    }
    
    if (((x > 0) & (y > 0)) | ((x < 0) & (y < 0))) {
      return abs_quotient;
    } else {
      return -abs_quotient;
    }
    // should not get here
    do Output.printString("Error: Math.divide should not get here\n\r");
    do Sys.halt();
    return 0;
  }

  function int sqrt(int x) {
    var int y;
    var int j;
    var int two_to_the_j;

    // Undefined for negative x
    // Report errors once error reporting available; for now, halt
    if (x < 0) {
      do Output.printString("Error: sqrt undefined for x < 0\n\r");
      do Sys.halt();
    }

    let j = 7;
    let y = 0;
    while (j > -1) {
      let two_to_the_j = two_to_the_power[j];

      if (~(Math.multiply((y + two_to_the_j), (y + two_to_the_j)) > x)) {
        let y = y + two_to_the_j;
      }

      let j = j - 1;
    }
    return y;
  }

  function int min(int x, int y) {
    if (x < y) {
      return x;
    } else {
      return y;
    }
    // should not get here
    do Output.printString("Error: Math.min should not get here\n\r");
    do Sys.halt();
    return 0;
  }

  function int max(int x, int y) {
    if (x > y) {
      return x;
    } else {
      return y;
    }
    // should not get here
    do Output.printString("Error: Math.max should not get here\n\r");
    do Sys.halt();
    return 0;
  }
}
