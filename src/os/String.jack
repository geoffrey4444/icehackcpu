class String {
  field int max_length;
  field int current_length;
  field Array char_array;
  field boolean owns_array;

  constructor String new(int max_number_of_chars) {
    if (max_number_of_chars < 1) {
      // Do not support strings with 0 or negative capacity
      do Output.printString("Error: string constructor requested < 1 character\n\r");
      do Sys.halt();
    }
    let max_length = max_number_of_chars;
    let current_length = 0;
    let char_array = Array.new(max_length);
    let owns_array = true;
    return this;
  }

  // Construct a string literal from the table of string constants
  constructor String newFromTable(int address, int length) {
    if ((address < 24600) | (address > (32767 - length))) {
      // Address outside of table
      do Output.printString("Error: string literal address out of bounds\n\r");
      do Sys.halt();
    }
    let max_length = length;
    let current_length = length;
    let char_array = address;
    let owns_array = false;
    return this;
  }

  method String appendChar(char c) {
    if (current_length = max_length) {
      // Can't add any more characters because string is full; halt
      // Later, implement error reporting
      do Output.printString("Error: string is full; cannot append character\n\r");
      do Sys.halt();
    }
    let char_array[current_length] = c;
    let current_length = current_length + 1;
    return this;
  }

  method int length() {
    return current_length;
  }

  method char charAt(int i) {
    if ((i < 0) | (~(i < current_length))) {
      // Requested a character beyond the end of the string; just return 0
      return 0;
    }
    return char_array[i];
  }

  method void setCharAt(int i, char c) {
    if ((i < 0) | (i > current_length)) {
      // Cannot insert; halt
      do Output.printString("Error: cannot insert character at invalid position\n\r");
      do Sys.halt();
    }    
    if (i = current_length) {
      if (~(current_length < max_length)) {
        // Cannot expand beyond max length
        do Output.printString("Error: cannot expand string beyond max length\n\r");
        do Sys.halt();
      }
      // append
      let current_length = current_length + 1;      
    }
    let char_array[i] = c;
    return;
  }

  method void eraseLastChar() {
    if (current_length = 0) {
      // String is already empty; just return empty string
      return;
    }
    let char_array[(current_length - 1)] = 0;
    let current_length = current_length - 1;
    return;
  }

  function char del() {
    return 127;
  }

  function char backSpace() {
    return 8;
  }

  function char doubleQuote() {
    return 34;
  }

  function char newLine() {
    return 10;
  }

  function char carriageReturn() {
    return 13;
  }

  method void clear() {
    let current_length = 0;
    return;
  }

  // Algorithm assumes value > 0. So take absolute value if not to make sure.
  // Does not check if string can hold the digit...up to the caller
  // to make sure string has the space!
  method void setValueDigits(int value) {
    var int remainder;

    if (~(value < 10)) {
      do setValueDigits(value / 10);
    }
    let remainder = value - (10 * (value / 10));
    do appendChar(48 + remainder);
    return;
  }

  method void setValue(int value) {
    do clear();
    do setValueDigits(value);
    return;
  }

  method void setValueDigitsIntThirtyTwo(int value_hi, int value_lo, IntThirtyTwo temp_a, IntThirtyTwo temp_b, IntThirtyTwo temp_c, IntThirtyTwo temp_d, IntThirtyTwo temp_e, IntThirtyTwo temp_f, IntThirtyTwo temp_g) {
    var int tenth_of_value_hi, tenth_of_value_lo;
    do temp_f.setHi(0);
    do temp_f.setLo(10);
    do temp_e.setHi(value_hi);
    do temp_e.setLo(value_lo);
    do IntThirtyTwo.unsigned_divide(temp_g, temp_e, temp_f, temp_a, temp_b, temp_c, temp_d);
    let tenth_of_value_hi = temp_g.hi();
    let tenth_of_value_lo = temp_g.lo();

    if (~((tenth_of_value_hi = 0) & (tenth_of_value_lo = 0))) {
      do setValueDigitsIntThirtyTwo(tenth_of_value_hi, tenth_of_value_lo, temp_a, temp_b, temp_c, temp_d, temp_e, temp_f, temp_g);
    }

    // temp values unknown after recursive call
    // remainder = temp_g = value - 10 * (value / 10)
    // temp_f = 10 * value / 10
    // temp_e = 10
    // temp_d = value / 10
    do temp_d.setHi(tenth_of_value_hi);
    do temp_d.setLo(tenth_of_value_lo);
    do temp_e.setHi(0);
    do temp_e.setLo(10);
    do IntThirtyTwo.unsigned_multiply(temp_f, temp_e, temp_d, temp_a, temp_b);
    do temp_c.setHi(value_hi);
    do temp_c.setLo(value_lo);
    do IntThirtyTwo.sub(temp_g, temp_a, temp_c, temp_f);

    // remainder must be between 0 and 9, inclusive, so look at remainder.lo()
    do appendChar(48 + (temp_g.lo()));
    return;
  }

  // note: temp_a, temp_b, temp_c, temp_d must not shadow value
  method void setValueIntThirtyTwo(IntThirtyTwo value, IntThirtyTwo temp_a, IntThirtyTwo temp_b, IntThirtyTwo temp_c, IntThirtyTwo temp_d, IntThirtyTwo temp_e, IntThirtyTwo temp_f, IntThirtyTwo temp_g) {
    do clear();
    do setValueDigitsIntThirtyTwo(value.hi(), value.lo(), temp_a, temp_b, temp_c, temp_d, temp_e, temp_f, temp_g);
    return;
  }

  method int intValue() {
    var int result, i, digit_value;
    let i = 0;
    while (i < current_length) {
      let digit_value = charAt(i) - 48;
      if ((digit_value < 0) | (digit_value > 9)) {
        return;
      }
      let result = (10 * result) + digit_value;
      let i = i + 1;
    }
    return result;
  }

  method void dispose() {
    let max_length = 0;
    let current_length = 0;
    do char_array.dispose();
    if (owns_array = true) {
      do Memory.deAlloc(this);
    }
    return;
  }

  method void print() {
    do Output.printArray(char_array, current_length);
    return;
  }

  // Read until string is full or a return or newline character is encountered
  // Does not yet respond to backsapce
  method void read(boolean echo_input_chars) {
    var int i, character;

    // Erase any character left hanging around before the read starts,
    // and set the status flag that there is no charater pending at start of
    // the read.
    if (Keyboard.available()) {
      do Keyboard.getChar();
    }    

    let i = 0;
    let current_length = 0;
    while (i < max_length) {
      if (Keyboard.available()) {
        let character = Keyboard.getChar();

        // If return, we're done reading characters
        if ((character = 10) | (character = 13)) {
          return; 
        }
        // If backspace, reduce the string length by one,
        // move left one character, and print a space over that character
        if ((character = String.backSpace()) | (character = String.del())) {
          // Backspace: remove a character from the string
          if (current_length > 0) {            
            let i = i - 1;
            let char_array[i] = 0;
            let current_length = current_length - 1;
            if (echo_input_chars = true) {
              do Output.printChar(String.backSpace());
              do Output.printChar(32);
              do Output.printChar(String.backSpace());
            }            
          }                    
        } else {    
          // Append character to string    
          let char_array[i] = character;
          let current_length = current_length + 1;
          let i = i + 1;
          if (echo_input_chars = true) {
            do Output.printChar(character);
          }
        }
      }
    }
  }
}
