class String {
  field int max_length;
  field int current_length;
  field Array char_array;

  constructor String new(int max_number_of_chars) {
    if (max_number_of_chars < 1) {
      // Do not support strings with 0 or negative capacity
      do Sys.halt();
    }
    let max_length = max_number_of_chars;
    let current_length = 0;
    let char_array = Array.new(max_length);
    return this;
  }

  method String appendChar(char c) {
    if (current_length = max_length) {
      // Can't add any more characters because string is full; halt
      // Later, implement error reporting
      do Sys.halt();
    }
    let char_array[current_length] = c;
    let current_length = current_length + 1;
    return this;
  }

  method int length() {
    return current_length;
  }

  method char charAt(int i) {
    if ((i < 0) | (~(i < current_length))) {
      // Requested a character beyond the end of the string; just return 0
      return 0;
    }
    return char_array[i];
  }

  method void setCharAt(int i, char c) {
    if ((i < 0) | (i > current_length)) {
      // Cannot insert; halt
      do Sys.halt();
    }    
    if (i = current_length) {
      if (~(current_length < max_length)) {
        // Cannot expand beyond max length
        do Sys.halt();
      }
      // append
      let current_length = current_length + 1;      
    }
    let char_array[i] = c;
    return;
  }

  method void eraseLastChar() {
    if (current_length = 0) {
      // String is already empty; just return empty string
      return;
    }
    let char_array[(current_length - 1)] = 0;
    let current_length = current_length - 1;
    return;
  }

  function char del() {
    return 127;
  }

  function char backSpace() {
    return 8;
  }

  function char doubleQuote() {
    return 34;
  }

  function char newLine() {
    return 10;
  }

  function char carriageReturn() {
    return 13;
  }

  method void dispose() {
    let max_length = 0;
    let current_length = 0;
    do char_array.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void print() {
    var int i;
    var char c;

    let i = 0;
    while (i < current_length) {
      // write character to address that TX monitors
      do Memory.poke(24577, char_array[i]);

      let i = i + 1;
    }

    return;
  }

  // Read until string is full or a return or newline character is encountered
  // Does not yet respond to backsapce
  method void read(boolean echo_input_chars) {
    var int i, character;

    // Erase any character left hanging around before the read starts,
    // and set the status flag that there is no charater pending at start of
    // the read.
    if (Keyboard.available()) {
      do Keyboard.getChar();
    }
    

    let i = 0;
    let current_length = 0;
    while (i < max_length) {
      if (Keyboard.available()) {
        let character = Keyboard.getChar();

        // If backspace, reduce the string length by one,
        // move left one character, and print a space over that character

        // If return, we're done reading characters
        if ((character = 10) | (character = 13)) {
          return; 
        }        
        let char_array[i] = character;
        let current_length = current_length + 1;
        let i = i + 1;
        if (echo_input_chars = true) {
          do Memory.poke(24577, character);
        }
      }
    }
  }
}
